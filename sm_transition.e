note
	description: "[
		Representation of a {SM_TRANSITION}
		]"
	design: "[
		Previously, the notion of a "transition" was merely a simple named-TUPLE. With the
		need to provide a UUID to the transition, it became necessary to create a class.
		This necessity came because transitions needed to be ID'd beyond simple start-stop
		values for the state IDs. It was impossible (on-the-face) to generate a unique hash
		of agents in the operation agents. So, it was decided that a simple UUID would be
		sufficient to "tag" each {SM_TRANSITION} created.
		]"

class
	SM_TRANSITION

create
	make

feature {NONE} -- Initialization

	make (a_start, a_stop: INTEGER; a_trigger_callback_agent: PROCEDURE [ANY, TUPLE [detachable ANY]]; a_operations: like operations; a_post_transition_operations: like post_transition_operations)
			-- `make' Current with `a_start' and `a_stop' state IDs, and with state assertion `a_operations' list.
		note
			design: "[
				Set the `start' and `stop' state ID's.
				Set the `operations' to the passed list of operation agents.
				Callback to the `a_trigger_callback_agent' with the agent to `do_operations'.
				Set the `uuid' of Current to a random UUID generated by `randomizer.uuid'.
				]"
		require
			non_zero: a_start > 0 and a_stop > 0
			has_operations: a_operations.count > 0
		do
			start := a_start
			stop := a_stop
			a_trigger_callback_agent.call (agent do_operations)
			operations := a_operations
			post_transition_operations := a_post_transition_operations
			uuid := randomizer.uuid
		ensure
			start_set: start = a_start
			stop_set: stop = a_stop
			operations_set: operations ~ a_operations
			post_operations_set: post_transition_operations ~ a_post_transition_operations
		end

feature {SM_MACHINE} -- Access

	start,
	stop: INTEGER
			-- `start' and `stop' state IDs for Current {SM_TRANSITION}.

	operations: ARRAY [PROCEDURE]
			-- List of `operations' for Current {SM_TRANSITION}.

	post_transition_operations: ARRAY [PROCEDURE]
			-- List of `post_transition_operations' for Current {SM_TRANSITION}.

	uuid: UUID
			-- `uuid' for Current {SM_TRANSITION}.

feature {NONE} -- Implementation: Basic Operations

	do_operations (a_data: detachable ANY)
			-- `do_operations' with `a_data' and execute all `operations' and `post_transition_operations'.
		do
			across operations as ic_ops loop ic_ops.item.call ([a_data]) end
			across post_transition_operations as ic_post_ops loop ic_post_ops.item.call ([a_data]) end
		end

feature {NONE} -- Implementation: Access

	randomizer: RANDOMIZER
			-- `randomizer' once'd to provide random {NATURAL} numbers for `uuid' initialization.
		once
			create Result
		end

end
